
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОписаниеТипаЧисло = Новый ОписаниеТипов("Число");
	
	URI = Параметры.ИдентификаторРесурса;
	
	ЗащищенноеСоединение = СтрНачинаетсяС(URI, "https://");
	Если ЗащищенноеСоединение Тогда
		Элементы.ДекорацияСхема.Заголовок = "https://";
	КонецЕсли;
	
	URI = СтрЗаменить(URI, "https://", "");
	URI = СтрЗаменить(URI, "http://", "");
	
	ПозицияОкончания = СтрНайти(URI, "?") - 1; // допущение: пароль не содержит символ '?'
	Если ПозицияОкончания < 0 Тогда
		ПозицияОкончания = СтрДлина(URI);
	КонецЕсли;
	
	ПозицияАдресаРесурса = СтрНайти(URI, "/");
	Если ПозицияАдресаРесурса > 0 Тогда
		АдресРесурса = Сред(URI, ПозицияАдресаРесурса, ПозицияОкончания - ПозицияАдресаРесурса + 1);
		ПозицияОкончания = ПозицияАдресаРесурса - 1;
	КонецЕсли;
	
	ПозицияПорта = СтрНайти(URI, ":", НаправлениеПоиска.СКонца, ПозицияОкончания);
	Если ПозицияПорта > 0 Тогда
		Порт = ОписаниеТипаЧисло.ПривестиЗначение(Сред(URI, ПозицияПорта + 1, ПозицияОкончания));
		ПозицияОкончания = ПозицияПорта - 1;
	Иначе
		Порт = ?(ЗащищенноеСоединение, 443, 80);
	КонецЕсли;
	
	Сервер = Лев(URI, ПозицияОкончания);
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ОК(Команда)
	Если ПустаяСтрока(Сервер) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Укажите имя хоста";
		Сообщение.Поле = "Сервер";
		Сообщение.Сообщить();
		
		Возврат;
	КонецЕсли;
	
	Закрыть(ИдентификаторРесурса());
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ЗащищенноеСоединениеПриИзменении(Элемент)
	Если ЗащищенноеСоединение Тогда
		Элементы.ДекорацияСхема.Заголовок = "https://";
		
		Если Порт = 80 Тогда
			Порт = 443;
		КонецЕсли;
	Иначе
		Элементы.ДекорацияСхема.Заголовок = "http://";
		
		Если Порт = 443 Тогда
			Порт = 80;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаКлиенте
Функция ИдентификаторРесурса()
	ЧастиURL = Новый Массив;
	ЧастиURL.Добавить(Элементы.ДекорацияСхема.Заголовок);
	ЧастиURL.Добавить(СокрЛП(Сервер));
	
	Если ЗащищенноеСоединение И Порт <> 443 ИЛИ НЕ ЗащищенноеСоединение И Порт <> 80 Тогда
		ЧастиURL.Добавить(":");
		ЧастиURL.Добавить(XMLСтрока(Порт));
	КонецЕсли;
	
	АдресРесурса = СокрЛП(АдресРесурса);
	Если НЕ ПустаяСтрока(АдресРесурса) И НЕ СтрНачинаетсяС(АдресРесурса, "/") Тогда
		ЧастиURL.Добавить("/");
	КонецЕсли;
	
	ЧастиURL.Добавить(АдресРесурса);
	
	Возврат СтрСоединить(ЧастиURL);
КонецФункции
#КонецОбласти
