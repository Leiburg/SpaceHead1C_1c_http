
&НаКлиенте
Перем ТекущееДоменноеИмя;


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗначенияАтрибутовПоУмолчанию = Новый Соответствие;
	ЗначенияАтрибутовПоУмолчанию.Вставить(РаботаСHttpПовтИсп.СвойствоПеченькаАдресРесурса(), "");
	ЗначенияАтрибутовПоУмолчанию.Вставить(РаботаСHttpПовтИсп.СвойствоПеченькаСрок(), '00010101');
	
	Дополнительно = Новый Структура("Печенье", Новый Соответствие);
	СвойстваПеченьки = РаботаСHttpПовтИсп.СвойстваКакАтрибуты();
	
	Для Каждого Получатель Из Параметры.Печенье Цикл
		Печеньки = Новый Соответствие;
		Для Каждого ПеченькоПолучателя Из Получатель.Значение Цикл
			Печенько = Новый Структура("Имя, Значение, Атрибуты", ПеченькоПолучателя.Значение.Имя, ПеченькоПолучателя.Значение.Значение, Новый Структура);
			Для Каждого СвойствоПеченьки Из РаботаСHttpПовтИсп.СвойстваКакАтрибуты() Цикл
				Печенько.Атрибуты.Вставить(
					СвойствоПеченьки.Ключ,
					?(
						ПеченькоПолучателя.Значение.Свойство(СвойствоПеченьки.Ключ),
						Новый Структура("Имя, Значение, Активно", ТРег(СвойствоПеченьки.Значение), ПеченькоПолучателя.Значение[СвойствоПеченьки.Ключ], Истина),
						Новый Структура("Имя, Значение, Активно", ТРег(СвойствоПеченьки.Значение), ЗначенияАтрибутовПоУмолчанию.Получить(СвойствоПеченьки.Ключ), Ложь)
					)
				);
			КонецЦикла;
			
			Печеньки.Вставить(ПеченькоПолучателя.Ключ, Печенько);
		КонецЦикла;
		
		Дополнительно.Печенье.Вставить(НормализованноеДоменноеИмя(Получатель.Ключ), Печеньки);
	КонецЦикла;
	
	Для Каждого Получатель Из Дополнительно.Печенье Цикл
		Стр = ДоменныеИмена.Добавить();
		Стр.ДоменноеИмя = Получатель.Ключ;
	КонецЦикла;
	
	ДоменныеИмена.Сортировать("ДоменноеИмя");
КонецПроцедуры


&НаКлиенте
Процедура ОК(Команда)
	фРезультат = Новый Соответствие;
	
	Для Каждого Получатель Из Дополнительно.Печенье Цикл
		Печеньки = Новый Соответствие;
		Для Каждого ПеченькоПолучателя Из Получатель.Значение Цикл
			Печенько = Новый Структура("Имя, Значение", ПеченькоПолучателя.Значение.Имя, ПеченькоПолучателя.Значение.Значение);
			Для Каждого Атрибут Из ПеченькоПолучателя.Значение.Атрибуты Цикл
				Если НЕ Атрибут.Значение.Активно Тогда
					Продолжить;
				КонецЕсли;
				
				Печенько.Вставить(Атрибут.Ключ, Атрибут.Значение.Значение);
			КонецЦикла;
			
			Печеньки.Вставить(ПеченькоПолучателя.Ключ, Новый ФиксированнаяСтруктура(Печенько));
		КонецЦикла;
		
		фРезультат.Вставить(Получатель.Ключ, Печеньки);
	КонецЦикла;
	
	Закрыть(фРезультат);
КонецПроцедуры

&НаКлиенте
Процедура ДоменныеИменаПриАктивизацииСтроки(Элемент)
	ПеченькиПолучателя.Очистить();
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Печеньки = Дополнительно.Печенье.Получить(Элемент.ТекущиеДанные.ДоменноеИмя);
	Если Печеньки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Для Каждого Печенько Из Печеньки Цикл
		Стр = ПеченькиПолучателя.Добавить();
		Стр.Ключ     = Печенько.Ключ;
		Стр.Имя      = Печенько.Значение.Имя;
		Стр.Значение = Печенько.Значение.Значение;
	КонецЦикла;
	
	ПеченькиПолучателя.Сортировать("Ключ");
КонецПроцедуры

&НаКлиенте
Процедура ДоменныеИменаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	ТекущееДоменноеИмя = ?(НоваяСтрока, Неопределено, Элемент.ТекущиеДанные.ДоменноеИмя);
КонецПроцедуры

&НаКлиенте
Процедура ДоменныеИменаПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	ДоменноеИмя = НормализованноеДоменноеИмя(Элемент.ТекущиеДанные.ДоменноеИмя);
	Если ТекущееДоменноеИмя <> Неопределено И ДоменноеИмя = ТекущееДоменноеИмя Тогда
		Возврат;
	КонецЕсли;
	
	НайденныеСтроки = ДоменныеИмена.НайтиСтроки(Новый Структура("ДоменноеИмя", ДоменноеИмя));
	ПозицияТекущейСтрокиВНайденных = НайденныеСтроки.Найти(ДоменныеИмена.НайтиПоИдентификатору(Элемент.ТекущаяСтрока));
	Если ПозицияТекущейСтрокиВНайденных <> Неопределено Тогда
		НайденныеСтроки.Удалить(ПозицияТекущейСтрокиВНайденных);
	КонецЕсли;
	
	Отказ = (НайденныеСтроки.Количество() > 0);
	Если Отказ Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Поле = "ДоменныеИмена.НайтиПоИдентификатору(Элемент.ТекущаяСтрока)";
		Сообщение.Текст = "Введите уникальное доменное имя";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДоменныеИменаПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	Элемент.ТекущиеДанные.ДоменноеИмя = НормализованноеДоменноеИмя(Элемент.ТекущиеДанные.ДоменноеИмя);
	Если ТекущееДоменноеИмя <> Неопределено И Элемент.ТекущиеДанные.ДоменноеИмя = ТекущееДоменноеИмя Тогда
		Возврат;
	КонецЕсли;
	
	Печеньки = ?(НоваяСтрока ИЛИ ТекущееДоменноеИмя = Неопределено, Новый Соответствие, Дополнительно.Печенье.Получить(ТекущееДоменноеИмя));
	
	Дополнительно.Печенье.Вставить(Элемент.ТекущиеДанные.ДоменноеИмя, Печеньки);
	Если ТекущееДоменноеИмя <> Неопределено Тогда
		Дополнительно.Печенье.Удалить(ТекущееДоменноеИмя);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДоменныеИменаПередУдалением(Элемент, Отказ)
	Дополнительно.Печенье.Удалить(Элемент.ТекущиеДанные.ДоменноеИмя);
КонецПроцедуры

&НаКлиенте
Процедура ДоменныеИменаПослеУдаления(Элемент)
	ОтобразитьРецепт();
КонецПроцедуры

&НаКлиенте
Процедура ПеченькиПолучателяПриАктивизацииСтроки(Элемент)
	АтрибутыПеченьки.Очистить();
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Рецепт = "";
		Возврат;
	КонецЕсли;
	
	Печеньки = Дополнительно.Печенье.Получить(Элементы.ДоменныеИмена.ТекущиеДанные.ДоменноеИмя);
	Печенько = Печеньки.Получить(ТекущиеДанные.Ключ);
	
	Для Каждого Атрибут Из Печенько.Атрибуты Цикл
		ЗаполнитьЗначенияСвойств(АтрибутыПеченьки.Добавить(), Атрибут.Значение);
	КонецЦикла;
	
	ОтобразитьРецепт(Печенько);
КонецПроцедуры


#Область ДОПОЛНИТЕЛЬНЫЕ_ПРОЦЕДУРЫ_И_ФУНКЦИИ
&НаКлиенте
Процедура ОтобразитьРецепт(Знач Печенько = Неопределено)
	Если Печенько = Неопределено Тогда
		ТекущиеДанныеПолучатели = Элементы.ДоменныеИмена.ТекущиеДанные;
		ТекущиеДанныеПеченькиПолучателя = Элементы.ПеченькиПолучателя.ТекущиеДанные;
		
		Если ТекущиеДанныеПолучатели = Неопределено ИЛИ ТекущиеДанныеПеченькиПолучателя = Неопределено Тогда
			Рецепт = "";
			Возврат;
		КонецЕсли;
		
		Печенько = Дополнительно.Печенье.Получить(ТекущиеДанныеПолучатели.ДоменноеИмя).Получить(ТекущиеДанныеПеченькиПолучателя.Ключ);
	КонецЕсли;
	
	ЧастиРецепта = Новый Массив;
	ЧастиПунктаРецепта = Новый Массив;
	
	ЧастиПунктаРецепта.Добавить(Печенько.Имя);
	Если Печенько.Значение <> Неопределено Тогда
		ЧастиПунктаРецепта.Добавить(Печенько.Значение);
	КонецЕсли;
	
	ЧастиРецепта.Добавить(СтрСоединить(ЧастиПунктаРецепта, "="));
	
	Для Каждого Атрибут Из Печенько.Атрибуты Цикл
		ЧастиПунктаРецепта.Очистить();
		
		Если НЕ Атрибут.Значение.Активно Тогда
			Продолжить;
		КонецЕсли;
		
		ЧастиПунктаРецепта.Добавить(Атрибут.Значение.Имя);
		Если ЗначениеЗаполнено(Атрибут.Значение.Значение) Тогда
			ЧастиПунктаРецепта.Добавить(
				?(
					Атрибут.Ключ = РаботаСHttpПовтИсп.СвойствоПеченькаСрок(),
					РаботаСHttpСлужебный.ДатаВHTTP(Атрибут.Значение.Значение),
					Атрибут.Значение.Значение
				)
			);
		КонецЕсли;
		
		ЧастиРецепта.Добавить(СтрСоединить(ЧастиПунктаРецепта, "="));
	КонецЦикла;
	
	Рецепт = СтрСоединить(ЧастиРецепта, "; ");
КонецПроцедуры

Функция  НормализованноеДоменноеИмя(Знач ДоменноеИмя)
	Возврат НРег(СокрЛП(ДоменноеИмя));
КонецФункции
#КонецОбласти
