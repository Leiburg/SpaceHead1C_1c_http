
// SPDX-License-Identifier: Apache-2.0+

#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.ЗаголовокФормы <> Неопределено Тогда
		Заголовок = Параметры.ЗаголовокФормы;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.АдресMonaco) Тогда
		Конфигурация = Новый Структура(
			"АдресMonaco, ФорматТекста, Текст",
			Параметры.АдресMonaco,
			Параметры.ФорматТекста,
			Параметры.Текст
		);
		Элементы.Основная.ТекущаяСтраница = Элементы.СтраницаФорматированныйТекст;
	Иначе
		РедактируемыйТекст = Параметры.Текст;
		Если Параметры.ЦветРедактируемогоТекста <> Неопределено Тогда
			Элементы.РедактируемыйТекст.ЦветТекста = Параметры.ЦветРедактируемогоТекста;
		КонецЕсли;
		Элементы.Основная.ТекущаяСтраница = Элементы.СтраницаРедактируемыйТекст;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПриОткрытии(Отказ)
	Если Конфигурация <> Неопределено Тогда
		ТекстMonaco = Конфигурация.АдресMonaco;
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийПоляРедактируемогоТекста
&НаКлиенте
Процедура РедактируемыйТекстИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	Элементы.РедактируемыйТекст.ЦветТекста = Новый Цвет;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийПоляФорматированногоТекста
&НаКлиенте
Процедура ФорматированныйТекстДокументСформирован(Элемент)
	View().setOption("autoResizeEditorLayout", Истина);
	View().hideScrollX();
	View().hideScrollY();
	View().setReadOnly(Ложь);
	View().eraseText();
	УстановитьФорматТекста(Конфигурация.ФорматТекста);
	View().setText(Конфигурация.Текст, Неопределено, Истина);
	View().setReadOnly(Истина);
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ОК(Команда)
	Закрыть(РедактируемыйТекст);
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаКлиенте
Функция View()
	Возврат Элементы.ТекстMonaco.Документ.defaultView;
КонецФункции

&НаКлиенте
Процедура УстановитьФорматТекста(Знач Формат)
	ИзвестныеФорматы = Новый Соответствие;
	Для Каждого Ф Из СтрРазделить("bsl,json,xml,html", ",") Цикл
		ИзвестныеФорматы.Вставить(Ф, Ф);
	КонецЦикла;
	ИзвестныйФормат = ИзвестныеФорматы.Получить(Формат);
	Если ИзвестныйФормат = Неопределено Тогда
		ИзвестныйФормат = "text";
	КонецЕсли;
	
	View().setLanguageMode(ИзвестныйФормат);
КонецПроцедуры
#КонецОбласти
